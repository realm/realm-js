buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$projectDir/../../react-native/android"
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.hasProperty("compileSdkVersion") ? rootProject.compileSdkVersion : 28
    buildToolsVersion rootProject.hasProperty("buildToolsVersion") ? rootProject.buildToolsVersion : "28.0.3"

    defaultConfig {
        minSdkVersion rootProject.hasProperty("minSdkVersion") ? rootProject.minSdkVersion : 16
        targetSdkVersion rootProject.hasProperty("targetSdkVersion") ? rootProject.targetSdkVersion : 28
    }
    buildTypes {
        debug {
            jniDebuggable true
        }
    }
}

String getAppId () {
    String myappId;
    try {
        String build = new File("$projectDir/../../../android/app/build.gradle").text
        def matcher = build =~ 'applicationId.*"'
        def appId = matcher.size() > 0 ? matcher[0].trim() - 'applicationId' - ~/\s/ : '';
        myappId = appId.replaceAll('"', '')
    } catch(all) {}
    return myappId
}

apply from: 'analytics.gradle'

import groovy.json.JsonSlurper

def getPackageVersion() {
    def inputFile = new File(buildscript.sourceFile.getParent() + "/../../package.json")
    def packageJson = new JsonSlurper().parseText(inputFile.text)
    return packageJson["version"]
}

task send(type: SendAnalyticsTask) {
    applicationId = getAppId()
    version = getPackageVersion().trim()
}

preBuild.dependsOn send

def dependencyType = "implementation"
try {
    project.getConfigurations().getByName("implementation")
} catch (UnknownConfigurationException e) {
    dependencyType = "compile" // Pre 3.0 Android Gradle Plugin
}

project.dependencies {
    add(dependencyType, fileTree(dir: 'libs', include: ['*.jar']))
    add(dependencyType, 'com.facebook.react:react-native:+')
}
